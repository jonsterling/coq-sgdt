Require Import ssrbool.
From extructures Require Import ord fmap fset.
From sgdt Require Import preamble impredicative guarded category functor adjunction.
From sgdt Require itree.

Set Bullet Behavior "Strict Subproofs".
Set Universe Polymorphism.

Module World.
  Definition world (T : Type) : Type := {fmap nat -> T}.

  Section World.
    Universe u.
    Context (T : Type@{u}).

    Definition world_leq (U V : world T) : Prop :=
      forall i, i \in domm U -> U i = V i.

    Fact world_leqR (U : world T) : world_leq U U.
    Proof. by []. Qed.

    Fact world_leqT (U V W : world T) : world_leq U V -> world_leq V W -> world_leq U W.
    Proof.
      move=> UV VW i hi.
      rewrite UV//= VW//=.
      apply/dommP.
      case eqn: (U i) => [x|].
      - by exists x; rewrite -eqn -UV//=.
      - case/dommP: hi=> x eqn'.
        rewrite eqn' in eqn.
        by discriminate eqn.
    Qed.

    Definition hom_mixin : Hom.mixin_of (world T).
    Proof. constructor; exact world_leq. Defined.

    Canonical hom : Hom.type.
    Proof. by esplit; apply: hom_mixin. Defined.

    Definition precat_mixin : Precategory.mixin_of hom.
    Proof. by build; apply: world_leqT. Qed.

    Definition precat : Precategory.type.
    Proof. by esplit; apply: precat_mixin. Defined.

    Definition cat_mixin : Category.mixin_of precat.
    Proof. by build; move=>*; cbn. Qed.

    Definition cat : Category.type.
    Proof. by esplit; apply: cat_mixin. Defined.
  End World.
End World.

Local Open Scope category_scope.

Definition ‚Ñ± (T : Type) : Type :=
  World.cat T ~> TYPE.cat@{_ Set}.

Definition ùíØ : Type.
Proof. by apply: Later.loeb=> /dlater; apply: ‚Ñ±. Defined.

Local Lemma ùíØ_unfold : ùíØ = ‚Ñ± (‚ñ∑ ùíØ).
Proof. by rewrite dlater_next_eq /ùíØ {1} Later.loeb_unfold. Qed.

Global Instance ùíØ_conn : Connective ùíØ (‚Ñ± (‚ñ∑ ùíØ)).
Proof. by build; rewrite -ùíØ_unfold; apply: iso_id. Qed.

Opaque ùíØ_conn.


Notation ùí≤ := (World.cat (‚ñ∑ ùíØ)).
Notation "ùíû+" := Cat[ùí≤, TYPE.cat@{_ Set}].
Notation "ùíû-[ E ]" := Cat[ùí≤^op, itree.ALG.cat E].


Module Ref.
  Section Ref.
    Context (A : ùíû+).

    Definition ob (w : ùí≤) : Set :=
      {i : nat | w i = Some (next: intro A)}.

    Definition rst (w1 w2 : ùí≤) (w12 : w1 ~> w2) : ob w1 -> ob w2.
    Proof.
      move=> [i hi]; exists i.
      abstract by rewrite -(w12 i) //=; apply/dommP; eauto.
    Defined.

    Definition prefunctor_mixin : Prefunctor.mixin_of ùí≤ (TYPE.cat@{_ Set}) ob.
    Proof. by build; apply: rst. Defined.

    Canonical prefunctor : Prefunctor.type ùí≤ (TYPE.cat@{_ Set}).
    Proof. by esplit; apply: prefunctor_mixin. Defined.

    Lemma functor_mixin : Functor.mixin_of _ _ prefunctor.
    Proof. by build; move=> *; apply: funE=> //=; case=> *; apply: subE. Qed.

    Canonical functor : Functor.type ùí≤ (TYPE.cat@{_ Set}).
    Proof. by esplit; apply: functor_mixin. Defined.

    Definition T : ùíû+ := functor.
  End Ref.
End Ref.


Definition heaplet (w w' : ùí≤) : Set :=
  forall i : nat,
    match w i with
    | None => True
    | Some A =>
        dlater (Later.map (fun X => (elim X : ‚Ñ± (‚ñ∑ ùíØ)) w') A)
    end.

Definition heap (w : ùí≤) := heaplet w w.

Module HEAP.
  Definition cat := Discrete.cat {w : ùí≤ & heap w}.
End HEAP.

Notation ‚Ñã := HEAP.cat.

Module PointwiseAlgAdjunction.
  Section Defs.
    Context (E : itree.Thy).
    Definition adj := PointwiseLiftingAdjunction.adj (ùí≤ ^op) _ _ (itree.EilenbergMoore.adj E).
  End Defs.
End PointwiseAlgAdjunction.

Module Œî.
  Module Psh.
    Section Defs.
      Context (A : Cat[ùí≤, SET.cat]).

      Definition ob : ‚Ñã -> SET.cat.
      Proof. by move/pi1; apply: A. Defined.

      Definition prefunctor_mixin : Prefunctor.mixin_of _ _ ob.
      Proof.
        build=> h1 h2 h12 x.
        by rewrite -h12.
      Defined.

      Canonical prefunctor : Prefunctor.type ‚Ñã SET.cat.
      Proof. by esplit; apply: prefunctor_mixin. Defined.

      Definition functor_mixin : Functor.mixin_of _ _ prefunctor.
      Proof.
        build=> h1 h2 h3 h12.
        move: h2 h12.
        apply: eq_ind.
        by move: h3; apply: eq_ind.
      Qed.

      Canonical functor : ‚Ñã ~~> SET.cat.
      Proof. by esplit; apply: functor_mixin. Defined.
    End Defs.
  End Psh.

  Definition prefunctor_mixin : Prefunctor.mixin_of _ _ Psh.functor.
  Proof.
    build=> A B.
    cbn.
    build.
    - case=> w h; apply: f.
    - abstract by build=> h1; apply: eq_ind.
  Defined.

  Canonical prefunctor : Prefunctor.type Cat[ùí≤,SET.cat] Cat[‚Ñã, SET.cat].
  Proof. by esplit; apply: prefunctor_mixin. Defined.

  Definition functor_mixin : Functor.mixin_of _ _ prefunctor.
  Proof.
    build.
    - by move=> ?; apply: NatTrans.ext.
    - by move=> ? ? ? ? ?; apply: NatTrans.ext.
  Qed.

  Canonical functor : Cat[ùí≤, SET.cat] ~~> Cat[‚Ñã, SET.cat].
  Proof. by esplit; apply: functor_mixin. Defined.
End Œî.

Module Œ£.
  Module Psh.
    Section Defs.
      Context (A : Cat[‚Ñã, TYPE.cat]).

      Definition ob : ùí≤^op -> TYPE.cat.
      Proof.
        move=> w.
        by refine {h : ‚Ñã & @hom ùí≤ w (pi1 h) √ó A h}.
      Defined.

      Definition prefunctor_mixin : Prefunctor.mixin_of (ùí≤^op) TYPE.cat ob.
      Proof.
        build=> w1 w2 w21 [h [œÅ a]].
        exists h; split.
        - by exact: (@seq ùí≤ _ _ _ w21 œÅ).
        - by exact: a.
      Defined.

      Canonical prefunctor : Prefunctor.type (ùí≤^op) TYPE.cat.
      Proof. esplit; apply: prefunctor_mixin. Defined.

      Definition functor_mixin : Functor.mixin_of _ _ prefunctor.
      Proof.
        build.
        - move=> w.
          apply: funE.
          case=> h [œÅ a].
          by cbn; rewrite (@seqL ùí≤).
        - move=> w1 w2 w3 w12 w23.
          apply: funE; case=> h [œÅ a].
          by cbn; rewrite (@seqA ùí≤).
      Qed.

      Canonical functor : ùí≤^op ~~> TYPE.cat.
      Proof. by esplit; apply: functor_mixin. Defined.
    End Defs.
  End Psh.

  Definition prefunctor_mixin : Prefunctor.mixin_of _ _ Psh.functor.
  Proof.
    build=> A B f.
    build.
    - move=> w; case=> h [œÅ a].
      exists h, œÅ.
      by apply: f.
    - by build.
  Defined.

  Canonical prefunctor : Prefunctor.type Cat[‚Ñã, TYPE.cat] Cat[ùí≤^op, TYPE.cat].
  Proof. by esplit; apply: prefunctor_mixin. Defined.

  Definition functor_mixin : Functor.mixin_of _ _ prefunctor.
  Proof.
    build.
    - by move=> ?; apply: NatTrans.ext.
    - by move=> ?????; apply: NatTrans.ext.
  Qed.

  Canonical functor : Cat[‚Ñã, TYPE.cat] ~~> Cat[ùí≤^op, TYPE.cat].
  Proof. by esplit; apply: functor_mixin. Defined.
End Œ£.


Module Œ£_Set.
  Definition functor : Cat[‚Ñã, SET.cat] ~~> Cat[ùí≤^op, SET.cat].
  Proof.
    apply: Compose.functor.
    - by apply/PointwiseLifting.functor/SetToType.functor.
    - apply: Compose.functor.
      + by apply: Œ£.functor.
      + by apply/PointwiseLifting.functor/TypeToSet.functor.
  Defined.
End Œ£_Set.
